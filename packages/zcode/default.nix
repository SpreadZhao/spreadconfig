{
  appimageTools,
  fetchurl,
  lib,
}:

let
  pname = "zcode";
  version = "3.2.2";
  src = fetchurl {
    url = "https://cdn-zcode.z.ai/zcode/electron/releases/${version}/ZCode-${version}-linux-x64.AppImage";
    hash = "sha256-QL9y2KCGtN3fHZ0IZgAaag9Uf4RHXE0FrFi+Ohd8Cz0=";
  };
  appimageContents = appimageTools.extractType2 {
    inherit pname version src;
  };
in
appimageTools.wrapType2 {
  inherit pname version src;

  extraInstallCommands = ''
    install -Dm444 "${appimageContents}/zcode.desktop" \
      "$out/share/applications/zcode.desktop"
    substituteInPlace "$out/share/applications/zcode.desktop" \
      --replace-fail "Exec=AppRun --no-sandbox %U" "Exec=zcode --no-sandbox %U"

    for size in 16 24 32 48 64 128 256 512 1024; do
      install -Dm444 "${appimageContents}/usr/share/icons/hicolor/''${size}x''${size}/apps/zcode.png" \
        "$out/share/icons/hicolor/''${size}x''${size}/apps/zcode.png"
    done
  '';

  meta = {
    description = "AI coding agent desktop application from Z.ai";
    homepage = "https://zcode.z.ai/en";
    license = lib.licenses.unfree;
    mainProgram = "zcode";
    platforms = [ "x86_64-linux" ];
    sourceProvenance = [ lib.sourceTypes.binaryNativeCode ];
  };
}
